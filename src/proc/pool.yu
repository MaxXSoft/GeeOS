public import proc.structs
public import proc.consts
public import proc.scheduler

import lib.except

// definition of thread status
public enum ThreadStatus {
  Unused,
  Ready,
  Running,
  Sleeping,
  Exited,
}

// definition of thread status with code
public struct ThreadStatusCode {
  status: ThreadStatus,
  code: usize,
}

public struct ThreadInfo {
  status: ThreadStatusCode,
  present: bool,
  thread: Thread var*,
}

// definition of thread pool
public struct ThreadPool {
  threads: ThreadInfo[MAX_THREAD_COUNT],
  scheduler: Scheduler,
}

def allocTid(this: ThreadPool&): Tid {
  var i = 0
  while i < MAX_THREAD_COUNT {
    if this.threads[i].status.status == ThreadStatus.Unused {
      return i as Tid
    }
    i += 1
  }
  panic("allocTid")
  0 as Tid
}

def getInfo(this: ThreadPool var&, tid: Tid): ThreadInfo var& {
  let info: ThreadInfo var& = this.threads[tid]
  assert(info.status.status != ThreadStatus.Unused,
         "thread does not exist")
  info
}

// initialize thread pool
public def init(this: ThreadPool var&, max_time: usize) {
  var i = 0
  while i < MAX_THREAD_COUNT {
    this.threads[i] = [ThreadInfo] {
      [ThreadStatusCode] {ThreadStatus.Unused},
      false,
      null as Thread var*,
    }
    i += 1
  }
  this.scheduler.init(max_time)
}

// add thread to pool
public def add(this: ThreadPool var&, thread: Thread var*) {
  let tid = this.allocTid()
  this.threads[tid] = [ThreadInfo] {
    [ThreadStatusCode] {ThreadStatus.Ready, 0 as usize},
    true,
    thread,
  }
  this.scheduler.push(tid)
}

// acquire a thread
public def acquire(this: ThreadPool var&, tid: Tid var&): Thread var* {
  if this.scheduler.pop(tid) {
    // get thread info of specific tid
    let info: ThreadInfo var& = this.getInfo(tid)
    // update thread info
    info.status.status = ThreadStatus.Running
    info.status.code = tid
    info.thread
  }
  else {
    null as Thread var*
  }
}

// retrieve thread to pool
public def retrieve(this: ThreadPool var&, tid: Tid, thread: Thread var*) {
  let info: ThreadInfo var& = this.getInfo(tid)
  if info.present {
    assert(info.thread == thread, "retrieve")
    // just schedule running threads
    // do not care sleeping threads until they are awakened
    if info.status.status == ThreadStatus.Running {
      info.status.status = ThreadStatus.Ready
      this.scheduler.push(tid)
    }
  }
  else {
    // thread exited, release resources
    info.status.status = ThreadStatus.Unused
    info.thread.del()
    info.thread = null as Thread var*
  }
}

// tell scheduler a tick passed
public def tick(this: ThreadPool var&): bool {
  this.scheduler.tick()
}

// mark specific thread as exited
public def exit(this: ThreadPool var&, tid: Tid, code: usize) {
  let info: ThreadInfo var& = this.getInfo(tid)
  info.status.status = ThreadStatus.Exited
  info.status.code = code
  info.present = false
  // tell scheduler thread exited
  this.scheduler.exit(tid)
}

// mark specific thread as sleeping
public def sleep(this: ThreadPool var&, tid: Tid) {
  let info: ThreadInfo var& = this.getInfo(tid)
  info.status.status = ThreadStatus.Sleeping
}

// wake up specific thread
public def wakeUp(this: ThreadPool var&, tid: Tid) {
  let info: ThreadInfo var& = this.getInfo(tid)
  info.status.status = ThreadStatus.Ready
  // push to scheduler, wating for next schedule
  this.scheduler.push(tid)
}
